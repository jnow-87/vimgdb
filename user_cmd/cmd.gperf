/* output declarations */
%struct-type
%omit-struct-type
%readonly-tables
%global-table
%includes
%language=C++

/* delimiter */
%delimiters=","

/* naming */
%define slot-name name
%define hash-function-name hash
%define lookup-function-name lookup
%define class-name user_cmd


/* c-code */
%{
	#include <common/log.h>
	#include <common/string.h>
	#include <gdb/gdb.h>
	#include <user_cmd/cmd.h>
	#include <string.h>
%}

/* struct-type definition */
struct user_cmd_t;

%%
# command	exec callback		cleanu callback			help callback		help text
test,		cmd_test_exec,		0,						cmd_test_help,		"test command echoing all given arguments"
help,		cmd_help_exec,		0,						0,					"print this help message"
inferior,	cmd_inferior_exec,	cmd_inferior_cleanup,	cmd_inferior_help,	"control inferior and its parameters"
break,		cmd_break_exec,		cmd_break_cleanup,		cmd_break_help,		"manage breakpoints"
exec,		cmd_exec_exec,		0,						cmd_exec_help,		"run control"
variable,	cmd_var_exec,		cmd_var_cleanup,		cmd_var_help,		"manage user defined variables"
callstack,	cmd_callstack_exec,	cmd_callstack_cleanup,	cmd_callstack_help,	"control layout and variables on the callstack"
register,	cmd_register_exec,	cmd_register_cleanup,	cmd_register_help,	"control target registers"
memory,		cmd_memory_exec,	cmd_memory_cleanup,		cmd_memory_help,	"display and modify memory"
evaluate,	cmd_evaluate_exec,	0,						cmd_evaluate_help,	"evaluate an expressions"
per,		cmd_per_exec,		cmd_per_cleanup,		cmd_per_help,		"display and modify memory mapped registers"
ui,			cmd_ui_exec,		0,						cmd_ui_help,		"control user interface parameter"
%%

/**
 * \brief	execute user command
 *
 * \param	cmdline		user command and arguments
 *
 * \return	0			sucess
 * 			-1			error
 */
int cmd_exec(char* line){
	int i;
	int argc, r;
	char** argv;
	FILE* sync;


	if(strsplit(strdeescape(line), &argc, &argv) != 0){
		ERROR("error pre-processing command string \"%s\"\n", line);
		return -1;
	}

	/* execute command */
	if(user_cmd::lookup(argv[0], strlen(argv[0])) <= 0){
		GDB("invalid user command \"%s\", trying direct execution by gdb\n", argv[0]);

		/* send unknown command to gdb */
		gdb->write(line, strlen(line));
		gdb->write((void*)"\n", 1);

		r = -1;
	}
	else{
		r = user_cmd::lookup(argv[0], strlen(argv[0]))->exec(argc - 1, argv);

		sync = fopen(argv[argc - 1], "w");

		if(sync != 0){
			fprintf(sync, "1\n");
			fclose(sync);
		}
		else
			ERROR("unable to open sync file \"%s\" - %s\n", argv[argc - 1], strerror(errno));
	}

	for(i=0; i<argc; i++)
		delete [] argv[i];
	delete [] argv;

	return r;
}
